package edu.fup.ims.examples.fire;
 
rule "When there is a fire turn on the sprinkler"
	when
		Fire($room : room)
		$sprinkler : Sprinkler( room == $room, on == false )
	then
		modify( $sprinkler ) { setOn( true ) };
		System.out.println( "Turn on the sprinkler for room " + $room.getName() );
end

rule "When the fire is gone turn off the sprinkler"
	when
		$room : Room( )
		$sprinkler : Sprinkler( room == $room, on == true )
		//$sprinkler : Sprinkler( $room : room, on == true )
		not Fire( room == $room )
	then
		modify( $sprinkler ) { setOn( false ) };
		System.out.println( "Turn off the sprinkler for room " + $room.getName() );
end

rule "Raise the alarm when we have one or more fires"
	when
		exists Fire()
		//Fire()
	then
		insert( new Alarm() );
		System.out.println( "Raise the alarm" );
end

rule "Cancel the alarm when all the fires have gone"
	when
		not Fire()
		$alarm : Alarm()
	then
		delete( $alarm );
		System.out.println( "Cancel the alarm" );
end

rule "Status output when things are ok"
	when
		not Alarm()
		not Sprinkler( on == true )
	then
		System.out.println( "Everything is ok" );
end

/*
rule "Show Sprinklers"
	when
		$room : Room()
		$sprinkler : Sprinkler( room == $room )
	then
		System.out.println( "room:" + $room.getName() +" sprinkler:" + $sprinkler.getRoom().getName() );
end
*/